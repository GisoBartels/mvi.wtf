plugins {
    id 'kotlin-multiplatform'
    id 'com.android.application'
    id 'kotlin-android-extensions'
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId 'wtf.mvi.example.number'
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName '1.0'
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    packagingOptions {
        pickFirst 'META-INF/kotlinx-coroutines-core.kotlin_module'
    }
    buildFeatures {
        compose true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
}

kotlin {
    android("android") {
        compilations.all {
            kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }
    iosX64("ios") {
        binaries {
            framework()
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation project(":core")
                implementation kotlin('stdlib-common')
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$kotlinCoroutinesVersion")
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        androidMain {
            dependencies {
                implementation project(":android")
                implementation kotlin('stdlib')
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion")
                implementation "androidx.activity:activity-ktx:1.0.0"
                implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0-rc03"
                implementation 'androidx.ui:ui-tooling:0.1.0-dev02'
                implementation 'androidx.ui:ui-layout:0.1.0-dev02'
                implementation 'androidx.ui:ui-material:0.1.0-dev02'
            }
        }
        androidTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        iosMain {
            dependencies {
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$kotlinCoroutinesVersion")
            }
        }
        iosTest {
        }
        all {
            languageSettings {
                useExperimentalAnnotation('kotlin.Experimental')
            }
        }
    }
}

task copyFramework {
    def buildType = project.findProperty('kotlin.build.type') ?: 'DEBUG'
    def target = project.findProperty('kotlin.target') ?: 'ios'
    dependsOn kotlin.targets."$target".binaries.getFramework(buildType).linkTask

    doLast {
        def srcFile = kotlin.targets."$target".binaries.getFramework(buildType).outputFile
        def targetDir = getProperty('configuration.build.dir')
        copy {
            from srcFile.parent
            into targetDir
            include 'app.framework/**'
            include 'app.framework.dSYM'
        }
    }
}
